#load "boolean.rea"
#load "nat.rea"

plusIdRight :: (n: Nat) -> n + 0 = n
{
 fork n
 {
  0      : truth,
  s(n69) : rewrite s(n69) + 0 => s(n69 + 0); rewrite plusIdRight(n69); truth,
  }
 }

print_debug plusIdRight;

plusSRight :: (n, m: Nat) -> n + s(m) = s(n+m)
{
 fork n
 {
  0    : rewrite 0 + s(m) => s(m); rewrite 0 + m => m; truth,
  s(n'):
  rewrite s(n') + s(m) => s(n' + s(m));
  rewrite plusSRight(n', m);
  rewrite s(n') + m => s(n'+m);
  truth,
  }
 }

plusCommutative :: (n, m: Nat) -> (n + m = m + n)
{
 fork n
 {
  0    : rewrite plusIdRight(m); truth,
  s(n1): rewrite plusCommutative(n1, m); rewrite plusSRight(m, n1); truth,
  }
 }

tester :: (a: Bool) -> (a & false = false)
{
 fork a
 {
  false: truth,
  true: truth,
  }
 }

;; check truth: False = (2*3 = 5);  ;; todo: this doesn't work due to typing of "=".
