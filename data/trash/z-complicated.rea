#load "natp.rea"

;; TODO To zoom out a bit, do we REALLY have to avoid zero?
;; Major problem is that equality would cause an issue: +(0) != -(0)
Z :: union
{0,
 +(v: Natp),
 -(v: Natp)}

+ :: fn(a,b: Z) -> Z
{fork a
 {0: return b,

  +:
  fork b
  {0: return a,
   +: return +(a.v + b.v),
   -:  ;; a positive, b negative
   fork (a.v)
   {1 : fork (b.v) {1: return 0, ++: return -(b.v.p)},
    ++: fork (b.v) {1: return +(a.v.p), ++: return +(a.v.p) + -(b.v.p)}}},

  -:
  fork b
  {0: return a,
   +: ;; a negative, b positve: symmetric case
   return b + a,
   -: ;; both negative
   return -(a.v + b.v)}}}

;; NOTE: this is negation, not to be confused with the actual constructor "-" for Z
- :: fn (a: Z) -> Z
{fork a {0: return 0,
         +: return -(a.v),
         -: return +(a.v)}}

* :: fn (a,b : Z) -> Z
{fork a
 {0: return 0,
  +: fork b {0: return 0,
             +: return +(a.v * b.v),
             -: return -(a.v * b.v)},
  -: return -(+(a.v) * b)}}

add0 :: (a: Z) -> a+0 = a
{fork a {0: , +: , -: }}

mul0 :: (a: Z) -> a*0 = 0
{fork a {0:, +:, -: }}

mul1Left :: (a: Z) -> +(1) * a = a
{fork a {0:,+:,-:}}

cancellation :: (a: Natp) -> +(a) + -(a) = 0
{fork a {1:,
         ++: ;; +(a.p) + -(a.p) = 0;
         rewrite cancellation(a.p)}}

;; todo rename this to addOpposite
cancellation :: (a: Z) -> a + -(a) = 0
{fork a {0: ,
         +: rewrite cancellation(a.v),
         -: rewrite cancellation(a.v)}}

mulSLeft :: (a: Natp, b: Z) -> (+(++(a)) * b) = b + +(a)*b
{fork b {0:, +:, -:}}

addCommutativeOpposite :: (a,b: Natp) -> +(a) + -(b) = -(b) + +(a)
{fork a {1:, ++: }}

negationOfSumOpposite :: (a,b: Natp) -> -(+(a) + -(b)) = +(b) + -(a) 
{fork a
 {1 : fork b {1:, ++:},
  ++: fork b {1:, ++: rewrite negationOfSumOpposite(a.p, b.p)}}}

negationOfSum :: (a, b: Z) -> -(a + b) = -(a) + -(b)
{fork a
 {0: ,
  +: fork (b) {0: , +:,
            -: ;; -(+(a.v) + -(b.v)) = +(b.v) + -(a.v);
            => +(b.v) + -(a.v) = +(b.v) + -(a.v) {negationOfSumOpposite}},
  -: fork(b) {0:,
              +: ;; -(+(b.v) + -(a.v)) = +(a.v) + -(b.v)
              => +(a.v) + -(b.v) = +(a.v) + -(b.v) {negationOfSumOpposite},
              -:}}}

addCommutativePositive :: (a: Natp, b: Z) -> +(a) + b = b + +(a)
{fork a
 {1: fork b {0:, +: rewrite add1(b.v), -:},
  ++: fork b {0:,
              +: rewrite addCommutative(b.v, ++(a.p));
              => norm,
              -:}}}

addCommutativeNegative :: (a: Natp, b: Z) -> -(a) + b = b + -(a)
{fork a
 {1: fork b {0:, +: , -: rewrite add1(b.v)},
  ++: fork b {0:,
              +: ,
              -: rewrite addCommutative(b.v, ++(a.p)); => norm}}}

addCommutative :: (a,b: Z) -> a+b = b+a
{fork(a)
 {0: rewrite add0(b),
  +: return addCommutativePositive(a.v, b),
  -: return addCommutativeNegative(a.v, b)}}

SToAdd1 :: (a: Natp) -> +(++(a)) = +(1) + +(a)
{fork a {1 :, ++: }}

<= :: fn (a,b: Natp) -> Bool
{fork a
 {1: return true, ++: fork b {1: return false, ++: return a.p <= b.p}}}

add1Associative :: (b,c: Z) -> (+(1)+b)+c = +(1)+b+c
{fork b {0:,
         +: ;; +(++(b.v)) + c = +(1) + +(b.v) + c;
         fork c {0:,+:,
                 -: ;; +(++(b.v)) + -(c.v) = +(1) + +(b.v) + -(c.v);
                 fork b.v
                 {1: ;; +(++(1)) + -(c.v) = +(1) + +(1) + -(c.v);
                  fork c.v {1:, ++:},
                  ++: ;; +(++(++(b.v.p))) + -(c.v) = +(1) + +(++(b.v.p)) + -(c.v);
                  fork c.v {1:,
                            ++: ;; +(++(b.v.p)) + -(c.v.p) = +(1) + +(b.v.p) + -(c.v.p);
                            rewrite SToAdd1(b.v.p);
                            ;; (+(1) + +(b.v.p)) + -(c.v.p) = +(1) + +(b.v.p) + -(c.v.p);
                            rewrite add1Associative(+(b.v.p), -(c.v.p))}}},
         -: ;; (+(1) + -(b.v)) + c = +(1) + -(b.v) + c;
         fork c {0: ;; (+(1) + -(b.v)) + 0 = +(1) + -(b.v);
                 rewrite add0(+(1) + -(b.v)),
                 +: ;; (+(1) + -(b.v)) + +(c.v) = +(1) + +(c.v) + -(b.v);
                 fork b.v {1 : ;; => +(c.v) = +(1) + +(c.v) + -(1);
                           fork c.v {1:, ++:},
                           ++: ;; => +(c.v) + -(b.v.p) = +(1) + +(c.v) + -(++(b.v.p));
                           fork c.v
                           {1: ,
                            ++: ;; => +(++(c.v.p)) + -(b.v.p) = +(1) + +(c.v.p) + -(b.v.p);
                            rewrite SToAdd1(c.v.p);
                            ;; => (+(1) + +(c.v.p)) + -(b.v.p) = +(1) + +(c.v.p) + -(b.v.p);
                            return add1Associative(+(c.v.p), -(b.v.p))}},
                 -: fork b.v {1 :, ++:}}}}

addPlus1Minus1Cancel :: (a: Z) -> -(1) + +(1) + a = a
{fork a {0,
         +,
         - fork a.v {1, ++}}}

addMinus1Associative :: (b,c: Z) -> (-(1)+b)+c = -(1)+b+c
{fork b {0,
         + fork b.v {1  fork c {0,+,- fork c.v {1, ++}},
                     ++ rewrite SToAdd1(b.v.p);
                     => +(b.v.p) + c = -(1) + +(1) + +(b.v.p) + c {add1Associative}
                     => +(b.v.p) + c = +(b.v.p) + c {addPlus1Minus1Cancel}},
         - fork b.v {1  fork c {0, + fork c.v {1, ++}, -},
                     ++ fork c {0,
                                + fork c.v
                                {1,
                                 ++
                                 => +(c.v.p) + -(1) + -(b.v.p) = -(1) + +(c.v.p) + -(b.v.p);
                                 => (-(1) + -(b.v.p)) + +(c.v.p)= -(1) + +(c.v.p) + -(b.v.p) {addCommutative};
                                 => -(1) + -(b.v.p) + +(c.v.p)= -(1) + +(c.v.p) + -(b.v.p) {addMinus1Associative};
                                 },
                                - }}}}

> :: fn(a, b: Z) -> Bool
{
 fork a
 {
  0: fork b {0: return false,
             +: return false,
             -: return true},
  +: fork b {0: return true,
             +: return a.v > b.v,
             -: return true},
  -: fork b {0: return false,
             +: return false,
             -: return b.v > a.v}
  }
 }

gtAntiReflexive :: (a: Z, p: a>a = true) -> False
#norm(p)
{
 fork a
 {
  0 return p,
  + return gtAntiReflexive(a.v, p),
  - return gtAntiReflexive(a.v, p)
  }
 }

successorIsLarger :: (a: Natp) -> +(++(a)) > +(a) = true
{
 fork a
 {
  1:,
  ++: return successorIsLarger(a.p),
  }
 }

gtTransitive :: (a, b, c: Z, P: a>b = true, Q: b>c = true) -> a > c = true
#norm(P,Q)
{
 fork a
 {
  0: fork b {0: fork P {},
             +: fork P {},
             -: fork c {0 return Q, + return Q, -}},
  -: fork b {0 fork P {},
             + fork P {},
             - fork c {0 return Q,
                       + return Q,
                       - return gtTransitive(c.v,b.v,a.v, Q,P)}},
  +: fork b {0 fork c {0, + fork Q {}, - },
             + fork c {0, + return gtTransitive(a.v,b.v,c.v, P,Q), - },
             - fork c {0, + fork Q {}, - }}
  }
 }

posSuccessorIsLarger :: (a: Natp) -> +(++(a)) > +(a) = true
{fork a {1, ++ return successorIsLarger(a.p)}}

negSuccessorIsSmaller :: (a: Natp) -> -(a) > -(++(a)) = true
{
 fork a {1, ++ return successorIsLarger(a.p)}
 }

addNegativeDecreases :: (a: Z, b: Natp) -> a > (a + -(b)) = true
{
 fork a
 {
  0: fork b {1: , ++:},
  +: fork b {1 : fork a.v {1:, ++: return successorIsLarger(a.v.p)},
             ++: fork a.v
             {1:,
              ++:
              lemma1: +(a.v.p) > (+(a.v.p) + -(b.p)) = true := addNegativeDecreases(+(a.v.p), b.p);
              lemma2: +(++(a.v.p)) > +(a.v.p) = true        := posSuccessorIsLarger(a.v.p);

              return gtTransitive(+(++(a.v.p)), +(a.v.p), +(a.v.p) + -(b.p), lemma2, lemma1)
              }},
  -: fork b {1  rewrite add1(a.v); return successorIsLarger(a.v),
             ++ return addIncreases(a.v, ++(b.p))}
  }
 }

addPositiveIncreases :: (a: Z, b: Natp) -> a + +(b) > a = true
{
 fork a
 {
  0,
  + return addIncreases(a.v, b),
  - fork b {1 fork a.v {1, ++ return successorIsLarger(a.v.p)},
            ++ fork a.v
            {1,
             ++ rewrite addCommutative(+(b.p), -(a.v.p));
             lem1: -(a.v.p) + +(b.p) > -(a.v.p) = true := addPositiveIncreases(-(a.v.p), b.p);
             lem2: -(a.v.p) > -(++(a.v.p)) = true := negSuccessorIsSmaller(a.v.p);
             return gtTransitive(-(a.v.p) + +(b.p), -(a.v.p), -(++(a.v.p)), lem1, lem2);
             }}
  }
 }

=? :: fn(a,b: Z) -> Bool
{
 fork a
 {
  0 fork b {0 return true,  + return false, - return false},
  + fork b {0 return false, + return a.v =? b.v, - return false},
  - fork b {0 return false, + return false, - return a.v =? b.v}
  }
 }

eqCorrect :: (a,b: Z, p: a =? b = true) -> a = b
#norm(p)
{
 fork a
 {
  0 fork b {0,
            + return p,
            - return p},
  + fork b {0 return p,
            + rewrite eqCorrect(a.v, b.v, p),
            - return p},
  - fork b {0 return p,
            + return p,
            - rewrite eqCorrect(a.v, b.v, p)}
  }
 }

add0Infer :: (a,b: Z, p: (a+b =? a) = true) -> b=0
#norm(p)
{
 fork b
 {
  0,
  +
  lem1 := addPositiveIncreases(a, b.v);
  p := eqCorrect(a + +(b.v), a, p);
  lem2 : a > a = true := prove {=> a + +(b.v) > a = true {p}; return lem1};
  return gtAntiReflexive(a, lem2),

  -
  lem1 := addNegativeDecreases(a, b.v);
  p := eqCorrect(a + -(b.v), a, p);
  lem2 : a > a = true := prove {=> a > a + -(b.v) = true {p}; return lem1};
  return gtAntiReflexive(a, lem2),
  }
 }

eqReflexive :: (a: Z) -> a =? a = true
{
 fork a
 {
  0,
  + return eqReflexive(a.v),
  - return eqReflexive(a.v)
  }
 }

eqSymmetric :: (a,b: Z, p: a =? b = true) -> b =? a = true
{
 rewrite eqCorrect(a,b,p);
 return eqReflexive(b);
 }

addPreservesGt :: (a,b,c: Z, p: a>b=true) -> a+c > b+c = true
#norm(p)
{
 fork a
 {
  0 fork b {0 fork p {},
            + fork p {},
            - rewrite addCommutative(-(b.v), c);
            return addNegativeDecreases(c, b.v)
            },
  + fork b {0 rewrite addCommutative(+(a.v), c); return addPositiveIncreases(c, a.v),
            + fork a.v {1 fork p {},
                        ++ fork b.v
                        {1 fork c {0,
                                   + rewrite addCommutative(a.v.p, c.v); return addIncreases(c.v, a.v.p),
                                   - fork c.v
                                   {1,
                                    ++ rewrite addCommutative(+(a.v.p), -(c.v.p));
                                    return addPositiveIncreases(-(c.v.p), a.v.p)}},
                         ++ fork c {0 return p,
                                    + return addPreservesGt(a.v.p,b.v.p,c.v, p),
                                    -
                                    lem1: +(++(a.v.p)) > +(++(b.v.p)) = true :=
                                    prove {=> a.v.p > b.v.p = true; return p};

                                    fork c.v
                                    {
                                     1 return p,
                                     ++
                                     lem1: +(a.v.p) > +(b.v.p) = true := prove {norm; return p};
                                     return addPreservesGt(+(a.v.p), +(b.v.p), -(c.v.p), lem1);
                                     }
                                    }}},
            -
            rewrite addCommutative(+(a.v), c);
            rewrite addCommutative(-(b.v), c);
            lem1 := addPositiveIncreases(c, a.v);
            lem2 := addNegativeDecreases(c, b.v);
            return gtTransitive(c + +(a.v), c, c + -(b.v), lem1, lem2);
            },
  - fork b {0 fork p {},
            + fork p {},
            - fork a.v {1 fork b.v
                        {1 fork p{},
                         ++ 
                         fork c {
                                 0,
                                 + fork c.v {1, ++ return addNegativeDecreases(+(c.v.p), b.v.p)},
                                 - fork c.v {1
                                             rewrite addCommutative(b.v.p, 1);
                                             return addIncreases(1, b.v.p),
                                             ++ rewrite addCommutative(b.v.p, ++(c.v.p));
                                             return addIncreases(++(c.v.p), b.v.p)}}},
                        ++ fork b.v
                        {
                         1 fork p{},
                         ++ fork c {
                                    0 return p,
                                    + fork c.v
                                    {1 return p,
                                     ++
                                     rewrite addCommutative(+(c.v.p), -(a.v.p));
                                     rewrite addCommutative(+(c.v.p), -(b.v.p));
                                     lem1: -(a.v.p) > -(b.v.p) = true := prove {=> norm; return p};
                                     return addPreservesGt(-(a.v.p), -(b.v.p), +(c.v.p), lem1);
                                     },
                                    -
                                    return addPreservesGt(b.v.p, a.v.p, c.v, p)}}}}}}

TotalOrder :: union($$a,$$b: Z)
{
 eq(p: a = b),
 gt(p: a > b = true),
 lt(p: b > a = true),
 }

totalOrder :: (a,b: Z) -> TotalOrder(a,b)
{
 fork a
 {
  0 fork b {0 return ctor[0](_),
            + return ctor[2](_),
            - return ctor[1](_)},
  + fork b {
            0 return ctor[1](_),
            +
            value_order := totalOrder(a.v, b.v);
            fork value_order
            {
             eq return ctor[0](prove{rewrite value_order.p}),
             gt return ctor[1](prove{norm; return value_order.p}),
             lt return ctor[2](prove{norm; return value_order.p}),
             },
            - return ctor[1](_)},
  -
  fork b {
          0 return ctor[2](_),
          + return ctor[2](_),
          -
          value_order := totalOrder(a.v, b.v);
          fork value_order
          {
           eq return ctor[0](prove{rewrite value_order.p}),
           gt return ctor[2](prove{norm; return value_order.p}),
           lt return ctor[1](prove{norm; return value_order.p}),
           }
          }
  }
 }

sumCancellationRight :: (a,b,c: Z, p: a+c = b+c) -> (a = b)
{
 order := totalOrder(a,b);
 fork order
 {
  eq: rewrite order.p,

  gt:
  lemma: a+c > b+c = true := addPreservesGt(a,b,c, order.p);
  inconsistency: a+c > a+c = true := prove{rewrite p; return lemma};
  falsehood := gtAntiReflexive(a+c, inconsistency);
  fork falsehood {}
  ,

  lt:
  lemma: b+c > a+c = true := addPreservesGt(b,a,c, order.p);
  inconsistency: b+c > b+c = true := prove{rewrite <- p; return lemma};
  falsehood := gtAntiReflexive(b+c, inconsistency);
  fork falsehood {}
  }
 }

sumCancellationLeft :: (a,b,c: Z, p: c+a = c+b) -> (a=b)
#norm(p)
{
 reduce : a+c = b+c := prove{rewrite addCommutative(a,c); rewrite addCommutative(b,c); return p};
 return sumCancellationRight(a,b,c, reduce);
 }

addAssociative :: (a,b,c: Z) -> (a+b)+c = a+b+c
{fork a
 {0:,
  +:
  fork a.v
  {1: fork b {0:,
              +: ;; => +(++(b.v)) + c = +(1) + +(b.v) + c;
              fork c
              {0:,
               +:,
               -: ;; => +(++(b.v)) + -(c.v) = +(1) + +(b.v) + -(c.v)
               => (+(1) + +(b.v)) + -(c.v) = +(1) + +(b.v) + -(c.v) {SToAdd1}
               => +(1) + +(b.v) + -(c.v) = +(1) + +(b.v) + -(c.v) {add1Associative}},

              -: ;; => (+(1) + -(b.v)) + c = +(1) + -(b.v) + c
              => +(1) + -(b.v) + c = +(1) + -(b.v) + c {add1Associative}},

   ++: ;; => (+(++(a.v.p)) + b) + c = +(++(a.v.p)) + b + c
   => ((+(1) + +(a.v.p)) + b) + c = +(++(a.v.p)) + b + c {SToAdd1}
   => ((+(1) + +(a.v.p)) + b) + c = (+(1) + +(a.v.p)) + b + c {SToAdd1}
   => (+(1) + +(a.v.p) + b) + c = (+(1) + +(a.v.p)) + b + c {add1Associative}
   => +(1) + (+(a.v.p) + b) + c = (+(1) + +(a.v.p)) + b + c {add1Associative}
   => +(1) + (+(a.v.p) + b) + c = +(1) + (+(a.v.p) + b + c) {add1Associative}
   rewrite addAssociative(+(a.v.p), b, c)}
  ,
  -:
  fork a.v
  {1:  fork b {0,
               + fork c
               {
                0 rewrite add0(+(b.v) + -(1)),
                + fork b.v {1, ++},
                - fork b.v
                {1  fork c.v {1, ++},
                 ++ fork c.v
                 {1,
                  ++
                  => +(b.v.p) + -(++(c.v.p)) = -(1) + -(c.v.p) + +(b.v.p) {addCommutative};
                  => +(b.v.p) + -(++(c.v.p)) = (-(1) + -(c.v.p)) + +(b.v.p) {addMinus1Associative};
                  }}},
               -
               => -(++(b.v)) + c = (-(1) + -(b.v)) + c {addMinus1Associative}},
   ++: fork b {0,
               + fork b.v
               {1
                => -(a.v.p) + c = (+(1) + c) + -(++(a.v.p)) {addCommutative}
                => -(a.v.p) + c = +(1) + c + -(++(a.v.p)) {addAssociative}
                => -(a.v.p) + c = +(1) + -(++(a.v.p)) + c {addCommutative}
                => -(a.v.p) + c = (+(1) + -(++(a.v.p))) + c {add1Associative}
                ,
                ++
                fork c
                {0 rewrite add0(+(b.v.p) + -(a.v.p)),
                 +
                 => (-(a.v.p) + +(b.v.p)) + +(c.v) = +(b.v.p + c.v) + -(a.v.p) {addCommutative}
                 => (-(a.v.p) + +(b.v.p)) + +(c.v) = -(a.v.p) + +(b.v.p + c.v) {addCommutative}
                 => (-(a.v.p) + +(b.v.p)) + +(c.v) = -(a.v.p) + +(b.v.p) + +(c.v);
                 return addAssociative(-(a.v.p), +(b.v.p), +(c.v))
                 ,
                 -
                 fork c.v
                 {1
                  => -(1) + +(b.v.p) + -(a.v.p) = +(b.v.p) + -(++(a.v.p)) {addCommutative}
                  => -(1) + -(a.v.p) + +(b.v.p) = +(b.v.p) + -(++(a.v.p)) {addCommutative}
                  => (-(1) + -(a.v.p)) + +(b.v.p) = +(b.v.p) + -(++(a.v.p)) {addMinus1Associative}
                  ,
                  ++
                  => -(++(c.v.p)) + (+(b.v.p) + -(a.v.p)) = -(++(a.v.p)) + +(b.v.p) + -(c.v.p) {addCommutative}
                  => (-(1) + -(c.v.p)) + +(b.v.p) + -(a.v.p) = (-(1) + -(a.v.p)) + +(b.v.p) + -(c.v.p);
                  => -(1) + (-(c.v.p) + +(b.v.p) + -(a.v.p)) = (-(1) + -(a.v.p)) + +(b.v.p) + -(c.v.p) {addMinus1Associative}
                  => -(1) + -(c.v.p) + +(b.v.p) + -(a.v.p) = -(1) + -(a.v.p) + +(b.v.p) + -(c.v.p) {addMinus1Associative}
                  => -(1) + -(c.v.p) + -(a.v.p) + +(b.v.p) = -(1) + -(a.v.p) + +(b.v.p) + -(c.v.p) {addCommutative}
                  => -(1) + (-(a.v.p) + +(b.v.p)) + -(c.v.p) = -(1) + -(a.v.p) + +(b.v.p) + -(c.v.p) {addCommutative}
                  rewrite addAssociative(-(a.v.p), +(b.v.p), -(c.v.p));
                  }}
                },
               -
               => (-(1) + -(a.v.p + b.v)) + c = (-(1) + -(a.v.p)) + -(b.v) + c {}
               => -(1) + -(a.v.p + b.v) + c = (-(1) + -(a.v.p)) + -(b.v) + c {addMinus1Associative}
               => -(1) + -(a.v.p + b.v) + c = -(1) + -(a.v.p) + -(b.v) + c {addMinus1Associative}
               => -(1) + (-(a.v.p) + -(b.v)) + c = -(1) + -(a.v.p) + -(b.v) + c {}
               rewrite addAssociative(-(a.v.p), -(b.v), c)
               }}
  }
 }

;; NOTE this is the easiest proof on the planet?
mulCommutative :: (a,b: Z) -> a*b = b*a
{fork a
 {0
  rewrite mul0(b),
  + fork b {0,
            + return mulCommutative(a.v, b.v),
            - return mulCommutative(a.v, b.v)} ,
  - fork b {0,
            + return mulCommutative(a.v, b.v),
            - return mulCommutative(a.v, b.v)}}}

mul1 :: (a: Z) -> a * +(1) = a
{fork a {0,
         + return mul1(a.v),
         - return mul1(a.v)}}

mulDistributiveHelper :: (a,b,c: Natp) -> +(a + b) + -(a + c) = +(b) + -(c)
{
 => (+(a) + +(b)) + -(a) + -(c)   = +(b) + -(c) {}
 => ((+(a) + +(b)) + -(a)) + -(c) = +(b) + -(c) {addAssociative}
 => (+(a) + +(b) + -(a)) + -(c)   = +(b) + -(c) {addAssociative}
 => (+(a) + -(a) + +(b)) + -(c)   = +(b) + -(c) {addCommutative}
 => ((+(a) + -(a)) + +(b)) + -(c) = +(b) + -(c) {addAssociative}
 => (0 + +(b)) + -(c)             = +(b) + -(c) {cancellation}
 }

mulDistributive :: (a,b,c: Z) -> a*c+b*c = (a+b)*c
{fork a
 {0: ,
  +: fork b
  {0: return add0((+(a.v) * c)),
   +:
   fork c
   {0: ,
    +:
    => +(a.v * c.v + b.v * c.v) = +((a.v + b.v) * c.v)
    => +((a.v + b.v) * c.v) = +((a.v + b.v) * c.v) {mulDistributive}
    ,
    -:
    => -(a.v * c.v + b.v * c.v) = -((a.v + b.v) * c.v)
    => -((a.v + b.v) * c.v) = -((a.v + b.v) * c.v) {mulDistributive}},

   -:
   fork c
   {
    0 => =(Z, 0, 0) {mul0},
    + ;; +(a.v * c.v) + -(b.v * c.v) = (+(a.v) + -(b.v)) * +(c.v)
    fork a.v
    {
     1
     fork b.v
     {
      1  return cancellation(c.v),
      ++ ;; +(c.v) + -(c.v + b.v.p * c.v) = -(b.v.p * c.v)
      => +(c.v) + -(c.v) + -(b.v.p * c.v) = -(b.v.p * c.v) {}
      => (+(c.v) + -(c.v)) + -(b.v.p * c.v) = -(b.v.p * c.v) {addAssociative}
      => 0 + -(b.v.p * c.v) = -(b.v.p * c.v) {cancellation}
      }
     ,
     ++ ;; +(c.v + a.v.p * c.v) + -(b.v * c.v) = (+(++(a.v.p)) + -(b.v)) * +(c.v)
     fork b.v
     {1 ;; +(c.v + a.v.p * c.v) + -(c.v) = +(a.v.p * c.v)
      => -(c.v) + +(c.v + a.v.p * c.v) = +(a.v.p * c.v) {addCommutative}
      => -(c.v) + +(c.v) + +(a.v.p * c.v) = +(a.v.p * c.v) {}
      => (-(c.v) + +(c.v)) + +(a.v.p * c.v) = +(a.v.p * c.v) {addAssociative}
      => (+(c.v) + -(c.v)) + +(a.v.p * c.v) = +(a.v.p * c.v) {addCommutative}
      => 0 + +(a.v.p * c.v) = +(a.v.p * c.v) {cancellation}
      ,
      ++ ;; +(c.v + a.v.p * c.v) + -(c.v + b.v.p * c.v) = (+(a.v.p) + -(b.v.p)) * +(c.v)
      fork c.v
      {1 ;; +(a.v.p * 1) + -(b.v.p * 1) = (+(a.v.p) + -(b.v.p)) * +(1)
       => +(a.v.p) + -(b.v.p * 1) = (+(a.v.p) + -(b.v.p)) * +(1) {mul1}
       => +(a.v.p) + -(b.v.p) = (+(a.v.p) + -(b.v.p)) * +(1) {mul1}
       => +(a.v.p) + -(b.v.p) = +(a.v.p) + -(b.v.p) {mul1}
       ,
       ++ ;; +(c.v.p + a.v.p * ++(c.v.p)) + -(c.v.p + b.v.p * ++(c.v.p)) = (+(a.v.p) + -(b.v.p)) * +(++(c.v.p))
       => +(a.v.p * ++(c.v.p)) + -(b.v.p * ++(c.v.p)) = (+(a.v.p) + -(b.v.p)) * +(++(c.v.p)) {mulDistributiveHelper}
       => +(a.v.p) * +(++(c.v.p)) + -(b.v.p) * +(++(c.v.p)) = (+(a.v.p) + -(b.v.p)) * +(++(c.v.p)) {}
       return mulDistributive(+(a.v.p), -(b.v.p), +(++(c.v.p)))
       }
      }
     }
    ,
    - ;; +(b.v * c.v) + -(a.v * c.v) = (+(a.v) + -(b.v)) * -(c.v)
    fork a.v
    {1 ;; +(b.v * c.v) + -(c.v) = (+(1) + -(b.v)) * -(c.v)
     fork b.v
     {1  return cancellation(c.v),
      ++ ;; +(c.v + b.v.p * c.v) + -(c.v) = +(b.v.p * c.v)
      => (+(c.v) + +(b.v.p * c.v)) + -(c.v) = +(b.v.p * c.v) {}
      => (+(b.v.p * c.v) + +(c.v)) + -(c.v) = +(b.v.p * c.v) {addCommutative}
      => +(b.v.p * c.v) + (+(c.v) + -(c.v)) = +(b.v.p * c.v) {addAssociative}
      => +(b.v.p * c.v) + 0 = +(b.v.p * c.v) {cancellation}
      }
     ,
     ++ ;; +(b.v * c.v) + -(c.v + a.v.p * c.v) = (+(++(a.v.p)) + -(b.v)) * -(c.v)
     fork b.v
     {1 ;; +(c.v) + -(c.v + a.v.p * c.v) = -(a.v.p * c.v)
      => +(c.v) + -(c.v) + -(a.v.p * c.v) = -(a.v.p * c.v) {}
      => (+(c.v) + -(c.v)) + -(a.v.p * c.v) = -(a.v.p * c.v) {addAssociative}
      => 0 + -(a.v.p * c.v) = -(a.v.p * c.v) {cancellation}
      ,
      ++ ;; +(c.v + b.v.p * c.v) + -(c.v + a.v.p * c.v) = (+(a.v.p) + -(b.v.p)) * -(c.v)
      => +(b.v.p * c.v) + -(a.v.p * c.v) = (+(a.v.p) + -(b.v.p)) * -(c.v) {mulDistributiveHelper}
      => +(b.v.p) * +(c.v) + +(a.v.p) * -(c.v) = (+(a.v.p) + -(b.v.p)) * -(c.v) {}
      }
     }
    }
   },
  -:
  }
 }
